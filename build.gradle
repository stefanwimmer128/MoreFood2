import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
        
        maven {
            url = "http://files.minecraftforge.net/maven"
        }
    }
    
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.10"
    id "com.github.johnrengelman.shadow" version "4.0.1"
    id "com.matthewprenger.cursegradle" version "1.1.0"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

apply plugin: "net.minecraftforge.gradle.forge"

file("build.properties").withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse(prop)
}

group = "eu.stefanwimmer128.morefood2"
archivesBaseName = "${config.modid}_${config.minecraft}"
version = config.version

sourceCompatibility = targetCompatibility = "1.8"

tasks.withType(JavaCompile) {
    sourceCompatibility = targetCompatibility = "1.8"
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

minecraft {
    version = "${config.minecraft}-${config.forge}"
    mappings = config.mappings
    
    runDir = "run"
    
    makeObfSourceJar = true
    
    replaceIn "src/api/kotlin/eu/stefanwimmer128/morefood2/api/MoreFood2API.kt"
    replace "#{VERSION}", config.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    from sourceSets.api.output
    
    classifier = "thin"
}

shadowJar {
    from sourceSets.api.output
    
    classifier = ""
}

sourceJar {
    from sourceSets.api.allSource
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}
tasks.reobfShadowJar.mustRunAfter tasks.shadowJar

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "shadow.morefood2"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar

task apiThinJar(type: Jar) {
    from sourceSets.api.output
    
    classifier = "api-thin"
}

task apiJar(type: ShadowJar) {
    from sourceSets.api.output
    
    configurations = [
            project.configurations.compile
    ]
    
    classifier = "api"
}

task relocateApiJar(type: ConfigureShadowRelocation) {
    target = tasks.apiJar
    prefix = "shadow.morefood2"
}
tasks.apiJar.dependsOn tasks.relocateApiJar

task deobfThinJar(type: Jar) {
    from sourceSets.api.output
    from sourceSets.main.output
    
    classifier = "deobf-thin"
}

task deobfJar(type: ShadowJar) {
    from sourceSets.api.output
    from sourceSets.main.output
    
    configurations = [
        project.configurations.compile
    ]
    
    classifier = "deobf"
}

task relocateDeobfJar(type: ConfigureShadowRelocation) {
    target = tasks.deobfJar
    prefix = "shadow.morefood2"
}
tasks.deobfJar.dependsOn tasks.relocateDeobfJar

artifacts {
    archives jar
    archives shadowJar
    archives apiThinJar
    archives apiJar
    archives deobfThinJar
    archives deobfJar
}

curseforge {
    apiKey = System.getenv("CURSE_API_TOKEN") ?: ""
    
    project {
        id = config.curse
        
        changelog = "Forge version: ${config.minecraft}-${config.forge}"
        
        releaseType = config.type
        
        addGameVersion config.minecraft
        
        mainArtifact shadowJar
        
        addArtifact sourceJar
        addArtifact deobfJar
        addArtifact apiJar
    }
}

def publishables = [
    "jar",
    "apiThinJar",
    "deobfThinJar",
]

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    pkg {
        repo = "maven"
        name = config.modid
        
        version {
            name = config.version
            desc = "Minecraft Forge: ${config.minecraft}-${config.forge}"
            vcsTag = "v${config.version}"
        }
    }
    publications = publishables
    override = true
    publish = true
}

publishing {
    publications {
        for (def publishable in publishables)
            "$publishable"(MavenPublication) {
                from components.java
                
                artifactId = config.modid + (publishable == "jar" ? "" : "-" + publishable.replace("ThinJar", "")) 
                version = config.version
                
                artifacts = [];
                artifact source: tasks[publishable], classifier: null
                artifact sourceJar
                
                pom {
                    name = "MoreFood 2"
                    description = ("Minecraft Forge: ${config.minecraft}-${config.forge}").toString()
                    url = "https://minecraft.curseforge.com/projects/morefood2"
                    licenses {
                        license {
                            name = "ISC"
                            url = "https://raw.githubusercontent.com/stefanwimmer128/morefood2/master/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "stefanwimmer128"
                            name = "Stefan Wimmer"
                            email = "info@stefanwimmer128.eu"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/stefanwimmer128/morefood2.git"
                        developerConnection = "scm:git:git@github.com:stefanwimmer128/morefood2.git"
                        url = "https://github.com/stefanwimmer128/morefood2"
                    }
                }
            }
    }
}
