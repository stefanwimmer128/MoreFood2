import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
        
        maven {
            url = "http://files.minecraftforge.net/maven"
        }
    }
    
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id "com.github.johnrengelman.shadow" version "4.0.1"
    id "com.matthewprenger.cursegradle" version "1.1.0"
}

apply plugin: "net.minecraftforge.gradle.forge"

file("build.properties").withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse(prop)
}

group = "eu.stefanwimmer128.morefood2"
archivesBaseName = "${config.modid}_${config.minecraft}"
version = config.version

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

compileApiJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileApiKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

minecraft {
    version = "${config.minecraft}-${config.forge}"
    mappings = config.mappings
    
    runDir = "run"
    
    makeObfSourceJar = true
    
    replaceIn "src/api/kotlin/eu/stefanwimmer128/morefood2/api/MoreFood2API.kt"
    replace "#{VERSION}", config.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    from sourceSets.api.output
    
    classifier = "thin"
}

shadowJar {
    from sourceSets.api.output
    
    classifier = ""
}

sourceJar {
    from sourceSets.api.allSource
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}
tasks.reobfShadowJar.mustRunAfter tasks.shadowJar

task apiJar(type: ShadowJar) {
    from sourceSets.api.output
    
    configurations = [
            project.configurations.compile
    ]
    
    classifier = "api"
}

task deobfJar(type: ShadowJar) {
    from sourceSets.api.output
    from sourceSets.main.output
    
    configurations = [
        project.configurations.compile
    ]
    
    classifier = "deobf"
}

artifacts {
    archives shadowJar
    archives apiJar
    archives deobfJar
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "shadow.morefood2"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar

task relocateApiJar(type: ConfigureShadowRelocation) {
    target = tasks.apiJar
    prefix = "shadow.morefood2"
}
tasks.apiJar.dependsOn tasks.relocateApiJar

task relocateDeobfJar(type: ConfigureShadowRelocation) {
    target = tasks.deobfJar
    prefix = "shadow.morefood2"
}
tasks.deobfJar.dependsOn tasks.relocateDeobfJar

curseforge {
    apiKey = System.getenv().CURSE_API_TOKEN ?: ""
    
    project {
        id = config.curse
        
        changelog = "Forge version: ${config.minecraft}-${config.forge}"
        
        releaseType = config.type
        
        addGameVersion config.minecraft
        
        mainArtifact shadowJar
        
        addArtifact sourceJar
        addArtifact deobfJar
        addArtifact apiJar
        addArtifact jar
    }
}
